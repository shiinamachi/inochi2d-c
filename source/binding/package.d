/*
    Copyright © 2022, Inochi2D Project
    Distributed under the 2-Clause BSD License, see LICENSE file.
    
    Authors: Luna Nielsen, seagetch
*/
module binding;
public import std.string : fromStringz, toStringz;
public import Inochi2D = inochi2d;
public import inochi2d.integration;
public import inochi2d.math;
public import core.runtime;
public import core.memory;

import core.sys.windows.windows;
import core.sys.windows.dll;
version(Posix) import core.sys.posix.stdlib : setenv, unsetenv;
import bindbc.opengl;
import core.stdc.stdlib;
import core.stdc.string;
import std.datetime.systime;
import utils;

import std.stdio;

// This needs to be here for Windows to link properly
version(Windows) {
    mixin SimpleDllMain;
} else {
    version = NotWindows;
}

alias i2DTimingFuncSignature = double function();

double currTime() {
    auto t = Clock.currTime;
    double result = (t.toUnixTime() + t.fracSecs.total!"msecs" * 0.001);
    return result;
}

// Everything here should be C ABI compatible
extern(C) export:

/**
    Initializes Inochi2D
*/
void inInit(i2DTimingFuncSignature func) {

    try {
        version(NotWindows) Runtime.initialize();
        version(yesgl) {
            version(Posix) {
                auto sessionTypePtr = getenv("XDG_SESSION_TYPE");
                string previousSessionType = null;
                if (sessionTypePtr !is null) {
                    previousSessionType = sessionTypePtr.fromStringz.idup;
                }

                bool needsWaylandSessionType = sessionTypePtr is null || strcmp(sessionTypePtr, "wayland") != 0;
                if (needsWaylandSessionType) {
                    setenv("XDG_SESSION_TYPE", "wayland", 1);
                }
                scope(exit) {
                    if (needsWaylandSessionType) {
                        if (sessionTypePtr is null) {
                            unsetenv("XDG_SESSION_TYPE");
                        } else {
                            setenv("XDG_SESSION_TYPE", previousSessionType.toStringz, 1);
                        }
                    }
                }
            }
            loadOpenGL();
        }
        if (func is null) {
            Inochi2D.inInit(&currTime);
        }
        else
            Inochi2D.inInit(func);
    } catch (Exception ex) {
        import std.stdio;
        writeln(ex);
    }
}

/**
    Updates the Inochi2D timing systems
*/
void inUpdate() {
    Inochi2D.inUpdate();
}

/**
    Uninitializes Inochi2D and cleans up everything
*/
void inCleanup() {
    version(yesgl) {
        unloadOpenGL();
    }
    version(NotWindows) Runtime.terminate();
}

/**
    Sets viewport
*/
void inViewportSet(int width, int height) {
    Inochi2D.inSetViewport(width, height);
}

/**
    Gets viewport size
*/
void inViewportGet(int* width, int* height) {
    int w, h;
    Inochi2D.inGetViewport(w, h);

    *width = w;
    *height = h;
}

version (yesgl) {
    /**
        Begins a scene render
    */
    void inSceneBegin() {
        Inochi2D.inBeginScene();
    }

    /**
        Ends a scene render
    */
    void inSceneEnd() {
        Inochi2D.inEndScene();
    }

    /**
        Draws Inochi2D scene
    */
    void inSceneDraw(float x, float y, float width, float height) {
        Inochi2D.inDrawScene(vec4(x, y, width, height));
    }
}

/**
    Runs function in a protected block that catches D exceptions.
*/
void inBlockProtected(void function() func) {
    try {
        func();
    } catch(Exception ex) {
        import std.stdio : writeln;
        writeln(ex);
    }
}

void inFreeMem(void* mem) {
    free(mem);
}

void inFreeArray(void** mem, size_t length) {
    for (size_t i = 0; i < length; i ++) {
        free(mem[i]);
    }
    free(mem);
}
